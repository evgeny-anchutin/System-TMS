
Функция ШтрихкодДокумента(ДокументСсылка) Экспорт
	
	Если ЗначениеЗаполнено(ДокументСсылка) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ШтрихкодыДокументов.Штрихкод КАК Штрихкод
		|ИЗ
		|	РегистрСведений.ШтрихкодыДокументов КАК ШтрихкодыДокументов
		|ГДЕ
		|	ШтрихкодыДокументов.ДокументСсылка = &ДокументСсылка";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Штрихкод = Выборка.Штрихкод;
		Иначе
			Штрихкод = "";
		КонецЕсли;
	Иначе
		Штрихкод = "";
	КонецЕсли;
	
	Возврат Штрихкод;
	
КонецФункции

Функция ГенерироватьШтрихкод(ДокументСсылка, Записать = Ложь) Экспорт
	
	// [Префикс(3)][Год(2)][НомерДокумента(7)][КонтрольныйСимвол(1)]
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЗаявкаНаПеревозку") Тогда
		Префикс = "241";
	КонецЕсли;
	
	НомерДокумента = Прав(ДокументСсылка.Номер, 7);
	Год = Формат(ДокументСсылка.Дата, "ДФ=yy");
	
	Штрихкод = Префикс + Год + НомерДокумента;
	Штрихкод = Штрихкод + КонтрольныйСимволEAN(Штрихкод);
	
	Если Записать Тогда
		УстановитьПривилегированныйРежим(Истина);
		МенеджерЗаписи = РегистрыСведений.ШтрихкодыДокументов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ДокументСсылка = ДокументСсылка;
		МенеджерЗаписи.Штрихкод = Штрихкод;
		МенеджерЗаписи.Записать(Истина);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат Штрихкод;
	
КонецФункции

// Функция вычисляет контрольный символ кода EAN
//
// Параметры:
//  ШтрихКод     - штрихкод (без контрольной цифры)
//  Тип          - тип штрихкода: 13 - EAN13, 8 - EAN8
//
// Возвращаемое значение:
//  Контрольный символ штрихкода
//
Функция КонтрольныйСимволEAN(ШтрихКод, Тип = 13) Экспорт

	Четн   = 0;
	Нечетн = 0;

	КоличествоИтераций = ?(Тип = 13, 6, 4);

	Для Индекс = 1 По КоличествоИтераций Цикл
		Если (Тип = 8) и (Индекс = КоличествоИтераций) Тогда
		Иначе
			Четн   = Четн   + Сред(ШтрихКод, 2 * Индекс, 1);
		КонецЕсли;
		Нечетн = Нечетн + Сред(ШтрихКод, 2 * Индекс - 1, 1);
	КонецЦикла;

	Если Тип = 13 Тогда
		Четн = Четн * 3;
	Иначе
		Нечетн = Нечетн * 3;
	КонецЕсли;

	КонтЦифра = 10 - (Четн + Нечетн) % 10;

	Возврат ?(КонтЦифра = 10, "0", Строка(КонтЦифра));

КонецФункции // КонтрольныйСимволEAN()
